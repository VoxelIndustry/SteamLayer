plugins {
    id "com.jfrog.artifactory" version "4.16.1"
    id "fabric-loom" version "0.4-SNAPSHOT"
    id "maven-publish"
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'maven-publish'
    apply plugin: 'fabric-loom'

    version = project.steamlayer_version
    group = "net.voxelindustry"

    compileJava.options.encoding = 'UTF-8'
    sourceCompatibility = targetCompatibility = "1.8"
    compileJava {
        sourceCompatibility = targetCompatibility = "1.8"
    }

    repositories {
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        mappings "net.fabricmc:yarn:${rootProject.yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"

        modImplementation "net.fabricmc.fabric-api:fabric-api:${rootProject.fabric_version}"

        compileOnly 'org.jetbrains:annotations:19.0.0'

        compileOnly 'org.projectlombok:lombok:1.18.12'
        annotationProcessor 'org.projectlombok:lombok:1.18.12'

        testCompile('org.junit.jupiter:junit-jupiter-api:5.6.1')
        testCompile('org.mockito:mockito-core:3.3.3')
        testCompile('org.mockito:mockito-junit-jupiter:3.3.3')
        testCompile('org.assertj:assertj-core:3.15.0')

        testRuntime('org.junit.jupiter:junit-jupiter-engine:5.6.1')

        testCompileOnly 'org.projectlombok:lombok:1.18.12'
        testAnnotationProcessor 'org.projectlombok:lombok:1.18.12'
    }

    processResources {
        inputs.property "version", project.version

        from(sourceSets.main.resources.srcDirs) {
            include "fabric.mod.json"
            expand "version": project.version
        }

        from(sourceSets.main.resources.srcDirs) {
            exclude "fabric.mod.json"
        }
    }

    artifactory {
        contextUrl = "${artifactory_contextUrl}"
        publish {
            repository {
                repoKey = 'voxelindustry'
                username = "${artifactory_user}"
                password = "${artifactory_password}"
                maven = true
            }
            defaults {
                publications('mavenJava')
                publishArtifacts = true
                publishPom = true
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifact(file("${project.buildDir}/libs/${archivesBaseName}-${project.steamlayer_version}.jar")) {
                    builtBy(remapJar)
                }
            }
        }

        repositories {
            mavenLocal()
        }
    }

    tasks.publish.dependsOn build
    artifactoryPublish.dependsOn build
}

archivesBaseName = "steamlayer"

subprojects {
    archivesBaseName = "${rootProject.archivesBaseName}-${project.name}"
}

project('steamlayer-common') {
    archivesBaseName = "steamlayer-common"

    dependencies {
    }
}

project('steamlayer-test') {
    archivesBaseName = "steamlayer-test"

    dependencies {
        compile('org.junit.jupiter:junit-jupiter-api:5.6.1')
        compile('org.mockito:mockito-core:3.3.3')
        compile('org.mockito:mockito-junit-jupiter:3.3.3')
        compile('org.assertj:assertj-core:3.15.0')
    }
}

project('steamlayer-inventory') {
    archivesBaseName = "steamlayer-inventory"

    dependencies {
        compile project(':steamlayer-common')
    }
}

project('steamlayer-network') {
    archivesBaseName = "steamlayer-network"

    dependencies {
        compile project(':steamlayer-common')
    }
}

project('steamlayer-container') {
    archivesBaseName = "steamlayer-container"

    dependencies {
        compile project(':steamlayer-inventory')
        compile project(':steamlayer-network')
    }
}

project('steamlayer-tile') {
    archivesBaseName = "steamlayer-tile"

    dependencies {
        compile project(':steamlayer-inventory')
        compile project(':steamlayer-network')
        compile 'org.hjson:hjson:3.0.0'
        include 'org.hjson:hjson:3.0.0'
    }
}

/*project('multiblock') {
    archivesBaseName = "steamlayer-multiblock"

    dependencies {
        compile project(':tile')
    }
}*/

project('steamlayer-grid') {
    archivesBaseName = "steamlayer-grid"

    dependencies {
        compile project(':steamlayer-tile')
    }
}

project('steamlayer-recipe') {
    archivesBaseName = "steamlayer-recipe"

    dependencies {
        compile project(':steamlayer-common')

        testCompile project(':steamlayer-test')
    }
}

project('steamlayer-math') {
    archivesBaseName = "steamlayer-math"

    dependencies {
        compile project(':steamlayer-common')

        testCompile project(':steamlayer-test')
    }
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }
subprojects.each { remapJar.dependsOn("${it.path}:remapJar") }


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(file("${project.buildDir}/libs/${rootProject.archivesBaseName}-${archivesBaseName}-${project.steamlayer_version}.jar")) {
                builtBy(remapJar)
            }
            pom.withXml {
                def depsNode = asNode().appendNode("dependencies")
                subprojects.each {
                    def depNode = depsNode.appendNode("dependency")
                    depNode.appendNode("groupId", it.group)
                    depNode.appendNode("artifactId", it.name)
                    depNode.appendNode("version", it.version)
                    depNode.appendNode("scope", "compile")
                }
            }
        }
    }

    repositories {
        mavenLocal()
    }
}

dependencies {
    afterEvaluate {
        subprojects.each {
            api it
            include it
        }
    }
}