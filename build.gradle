buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven {
            url 'http://maven.ferenyr.info/artifactory/plugins-release'
            credentials {
                username = "${artifactory_user}"
                password = "${artifactory_password}"
            }
        }
    }
    dependencies {
        classpath('net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT')
    }
}

plugins {
  id "com.jfrog.artifactory" version "4.7.5"
}

subprojects {
    apply plugin: 'net.minecraftforge.gradle.forge'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.artifactory'

    version = "0.4.1"
    group = "net.voxelindustry"

    compileJava.options.encoding = 'UTF-8'
    sourceCompatibility = targetCompatibility = "1.8"
    compileJava {
        sourceCompatibility = targetCompatibility = "1.8"
    }

    artifactory {
        contextUrl = "${artifactory_contextUrl}"
        publish {
            repository {
                repoKey = 'voxelindustry'
                username = "${artifactory_user}"
                password = "${artifactory_password}"
                maven = true
            }
            defaults {
                publications ('mavenJava')
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifactId archivesBaseName
                artifact source: sourcesJar, classifier: 'sources', extension: 'jar'
            }
        }
    }

    artifactoryPublish.dependsOn build

    minecraft {
        version = "1.12.2-14.23.5.2814"
        runDir = "run"

        mappings = "stable_39"
    }

    repositories {
    }

    processResources {
        inputs.property "version", project.version
        inputs.property "mcversion", project.minecraft.version

        from(sourceSets.main.resources.srcDirs) {
            include 'mcmod.info'
            expand 'version': project.version, 'mcversion': project.minecraft.version
        }
        from(sourceSets.main.resources.srcDirs) {
            exclude 'mcmod.info'
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok:1.18.4'

        testCompile 'org.assertj:assertj-core-java8:1.0.0m1'
        testCompile 'junit:junit:4.12'
        testCompile 'org.mockito:mockito-core:2.8.47'
    }
}

project('common') {
    archivesBaseName = "steamlayer-common"

    dependencies {
    }
}

project('inventory') {
    archivesBaseName = "steamlayer-inventory"

    dependencies {
        compile project(':common')
    }
}

project('network') {
    archivesBaseName = "steamlayer-network"

    dependencies {
        compile project(':common')
    }
}

project('container') {
    archivesBaseName = "steamlayer-container"

    dependencies {
        compile project(':inventory')
        compile project(':network')
    }
}

project('tile') {
    archivesBaseName = "steamlayer-tile"

    dependencies {
        compile project(':inventory')
        compile project(':network')
    }
}

project('grid') {
    archivesBaseName = "steamlayer-grid"

    dependencies {
        compile project(':tile')
    }
}

project('core') {
    archivesBaseName = "steamlayer-core"

    dependencies {
        compile project(':container')
        compile project(':tile')
        compile project(':grid')
    }
}

subprojects.each { subproject -> evaluationDependsOn(subproject.path) }